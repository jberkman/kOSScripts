// ddStabilize.ks - Stabilize vessel.
// Copyright Â© 2015 jacob berkman
// This file is distributed under the terms of the MIT license.

@lazyglobal off.

clearscreen.
print "DunaDirect Stabilize! v1.0".

local debug is false.

if debug {
  clearscreen.
  //     0         1         2         3         4         5         6
  //     0123456789012345678901234567890123456789012345678901234567890123456789
  print "          yaw       pitch     roll".
  print "velocity:".
  print " control:".
  print "Stabilizing vessel...".
}

sas off.
rcs off.
unlock steering.
unlock throttle.  

local control to ship:control.
set control:rotation to V(0, 0, 0).

local lock yawVel to vdot(angularVel, facing:topVector).
local lock pitchVel to -vdot(angularVel, facing:starVector).
local lock rollVel to -vdot(angularVel, facing:foreVector).

local yawPID is PIDLoop(1, 0, 0, -1, 1).
local pitchPID is PIDLoop(1, 0, 0, -1, 1).
local rollPID is PIDLoop(1, 0, 0, -1, 1).

until angularVel:mag < 0.01 {
  set control:rotation to V(yawPID:update(time:seconds, yawVel), pitchPID:update(time:seconds, pitchVel), rollPID:update(time:seconds, rollVel)).

  if debug {
    print round(yawVel, 3) at(10, 1).
    print round(pitchVel, 3) at(20, 1).
    print round(rollVel, 3) at(30, 1).

    print round(control:yaw, 3) at(10, 2).
    print round(control:pitch, 3) at(20, 2).
    print round(control:roll, 3) at(30, 2).
  }
  
  wait 0.05.
}

set control:neutralize to true.
print "Stabilization complete.".
