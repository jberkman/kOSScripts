// launch.ks - Launch into a parking suborbital trajectory.
// Copyright Â© 2015 jacob berkman
// This file is distributed under the terms of the MIT license.

@lazyglobal off.

clearscreen.
print "DunaDirect Launch! v2.0".

parameter inclination is 0, rendezvous is false, goalAltitude is -1.

run once lib_duna_direct.

function orbitalVelocity {
  parameter orbitable.
  parameter altitude.
  parameter a is orbitable:obt:semiMajorAxis.

  local r is altitude + orbitable:body:radius.

  return sqrt(orbitable:body:mu * ((2 / r) - (1 / a))).  
}

local launchHeading is 90 - inclination.
local launchAltitude is body:atm:height + 10000.
if rendezvous {
	set launchAltitude to target:orbit:semiMajorAxis - body:radius.
}
if goalAltitude > 0 {
	set launchAltitude to goalAltitude.
}

print "Flight computer online.".
set ship:control:pilotmainthrottle to 0.
lock throttle to 1.
wait until verticalSpeed > 1.

print "Liftoff!".
wait until verticalSpeed > 10.

local lock lookAt to heading(launchHeading, 90).
local lock lookUp to heading(launchHeading, -45).
lock steering to lookdirup(lookAt:vector, lookUp:vector).

if body:atm:exists {
  wait until verticalSpeed > 50.

  print "Initiating gravity turn.".
  lock targetPitch to 90 - 90 * min(1, apoapsis / (body:atm:height - 10000)).
  lock lookAt to heading(launchHeading, max(targetPitch, -8 * pitchForVec(ship, ship:velocity:orbit))).

  until altitude > body:atm:height {
    set warp to 0.
    wait until warp = 0.
    lock throttle to 1.
    until apoapsis > launchAltitude {
      wait until apoapsis > launchAltitude or maxThrust = 0.
      until maxThrust > 0 {
        wait 2.
        stage.
      }
    }
    lock throttle to 0.
    wait until altitude > body:atm:height or apoapsis < body:atm:height + 5000.
  }
} else {
	wait until apoapsis > altitude + 1000 - alt:radar.
  lock lookAt to heading(launchHeading, 22.5).
  wait until apoapsis > launchAltitude.
}

lock throttle to 0.

if rendezvous {
	run dd_rendezvous.
} else {
  local goalSemiMajorAxis is body:radius + apoapsis.
  local initialVelocity is orbitalVelocity(ship, apoapsis).
  local goalVelocity is orbitalVelocity(ship, apoapsis, goalSemiMajorAxis).
  local deltaV is goalVelocity - initialVelocity.

  local burnTime is deltaVBurnTime(deltaV).
  lock burnStartTime to time:seconds + eta:apoapsis - burnTime / 2.

  print "burn time: " + round(burnTime) + " dV:" + round(deltaV).

  wait until time:seconds >= burnStartTime - 30.
  set warp to 0.
  lock burnPitch to -pitchForVec(ship, ship:prograde:forevector).
  lock steering to lookdirup(heading(launchHeading, burnPitch):vector, heading(launchHeading, -45):vector).

  wait until time:seconds >= burnStartTime.
  steerToDir().
  lock throttle to 1.

  wait until obt:semiMajorAxis >= goalSemiMajorAxis.

  lock throttle to 0.
  unlock steering.
  unlock throttle.

  print "Burn complete.".
}
